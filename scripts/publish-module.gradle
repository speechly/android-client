apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

version '0.1.12'
group 'com.speechly'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId this.group
                artifactId "android-client"
                version this.version
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = "android-client"
                    description = 'Speechly official Android SDK'
                    url = 'https://github.com/speechly/android-client'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/speechly/android-client/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'igorp'
                            name = 'Igor Poltavskiy'
                            email = 'igorp@speechly.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/speechly/android-client.git'
                        developerConnection = 'scm:git:ssh://github.com/speechly/android-client.git'
                        url = 'https://github.com/speechly/android-client/tree/main'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}