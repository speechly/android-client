plugins {
    id 'com.android.library'
    id 'org.jetbrains.dokka'
    id 'kotlin-android'
    id 'maven-publish'
}

version '0.1.8'
group 'com.speechly'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName this.version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    tasks {
        dokkaGfm {
            outputDirectory = new File("${project.rootDir}/docs/")
            doLast {
                new File("${project.rootDir}/docs/client/index.md").renameTo("${project.rootDir}/docs/client/README.md")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        testOptions {
            unitTests.all {
                useJUnitPlatform()
            }
        }
    }

    sourceSets["main"].java.srcDir("src/main/kotlin")

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    def activity_version = "1.2.3"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation group: 'com.speechly', name: 'api', version: '0.0.6'
    implementation "io.grpc:grpc-okhttp:1.35.0"
    implementation "com.beust:klaxon:5.5"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
    testImplementation "io.strikt:strikt-core:0.31.0"

    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    implementation 'androidx.fragment:fragment-ktx:1.3.4'
    implementation "androidx.activity:activity-ktx:$activity_version"
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/client-release.aar")
            artifact(sourceJar)
            groupId this.group
            artifactId "android-client"
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri('https://maven.pkg.github.com/speechly/api')
            def (githubUser, githubToken) = getGithubCredentials()
            credentials {
                username = githubUser
                password = githubToken
            }
        }
    }
}

//bintray {
//    user = project.hasProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
//    key = project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
//    publications = ['Production']
//    configurations = ['archives']
//    override = true
//    pkg {
//        repo = 'maven'
//        name = 'android-client'
//        userOrg = 'speechly'
//        description = "Android client for Speechly SLU API"
//        publish = true
//        publicDownloadNumbers = true
//        licenses = ['MIT']
//        vcsUrl = 'https://github.com/speechly/android-client.git'
//        version {
//            name = this.version
//            desc = "Android-client ${this.version}"
//            released = new Date()
//            vcsTag = this.version
//        }
//    }
//}

